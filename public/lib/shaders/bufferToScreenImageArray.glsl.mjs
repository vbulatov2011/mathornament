export const bufferToScreenImageArray = 
/*glsl*/`
in vec2 vUv;

layout(location = 0) out vec4 outColor;

uniform float u_pixelSize;

uniform lowp sampler2DArray uImageArray;
uniform int uNumImages;

uniform vec2 uBufCenter;
uniform vec2 uBufScale;
uniform int uDataSource;

uniform sampler2D uGroupData;
uniform int uIterations;
uniform bool uSymmetry;
uniform int uInterpolation;
uniform float uTransparency;

void main() {

    int groupOffset = 0;
    int inDomain = 0;
    int refcount = 0;

    vec2 pp = vUv;

    float scale = 1.;

    #ifdef HAS_SPHERICAL_PROJECTION
    if(u_sphericalProjectionEnabled){
        float sdist = makeSphericalProjection(pp, scale);
        if(sdist > 0.) {
            outColor = vec4(0,0,0,0);
            return;
        }
    }
    #endif

    #ifdef HAS_PROJECTION
    makeProjection(pp, scale);
    #endif

    vec3 wpnt = vec3(pp, 0.);

    if(uSymmetry){
        iToFundamentalDomainSampler(wpnt, uGroupData, groupOffset, inDomain, refcount, scale, uIterations);
    }
    if(uSymmetry && inDomain == 0) {
        outColor = vec4(0,0,0,0);
        return;
    }

    // Map world point into sampler coordinates.
    vec2 tc = cMul(uBufScale, (wpnt.xy - uBufCenter));
    vec2 tpnt = tc + vec2(0.5, 0.5);
    tc = abs(tc);
    float sdb = max(tc.x, tc.y) - 0.5;
    float blurWidth = u_pixelSize * 0.5;
    float mask = 1. - smoothstep(-blurWidth, blurWidth, sdb);

    // Composite all layers using Porter-Duff "over" (premultiplied).
    // sampler2DArray allows real loop indexing — no unrolling needed.
    vec4 color = vec4(0.0);
    for (int i = 0; i < uNumImages; i++) {
        vec4 layer = texture(uImageArray, vec3(tpnt, float(i)));
        color = overlayColor(color, layer);
    }

    outColor = color * mask * (1. - uTransparency);
}
`;
